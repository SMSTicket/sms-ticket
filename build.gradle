/*
 * Copyright 2015 AVAST Software s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {

        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }

        mavenCentral()
        jcenter()
        repositories {
            maven { url 'https://maven.fabric.io/public' }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.novoda:gradle-android-command-plugin:1.5.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'io.fabric.tools:gradle:1.21.2'
    }
}

allprojects {
    repositories {

        maven { url 'https://maven.fabric.io/public'  }
        mavenLocal()
        jcenter()
    }

    // *** common methods ***

    /**
     * Will return version from properties file and replace -SNAPSHOT by GIT commit hash
     * to recognize origin commit for the every build.
     */
    project.ext.evalVersionName = {
        if (isIDEBuild()) {
            logger.info("IDE build");
            return "dev"
        } else if (isSnapshotBuild()) {
            logger.info("SNAPSHOT build");
            if (checkUncommitedChanges()) {
                logger.warn("You have uncommited changes!")
            }
            return project.VERSION.toUpperCase().replace("SNAPSHOT", getGitHash())
        }
        return project.VERSION;
    }

    /**
     * Return true if version of the project end by SNAPSHOT
     */
    project.ext.isSnapshotBuild = {
        return project.VERSION.toUpperCase().contains("SNAPSHOT");
    }

    /**
     * Creates versionCode from number of commits in GIT
     */
    project.ext.evalVersionCode = {
        // number of commits from HEAD to oldest one, useful for releasing from one branch only
        def p = Runtime.getRuntime().exec("git rev-list HEAD --count")
        // number of commit in all branches together (also dead one)
        //def p = Runtime.getRuntime().exec("git rev-list --all --count")

        def result = p.waitFor()
        if (result != 0) {
            return 0 // no git revisions
        }
        return p.getInputStream().readLines().get(0).toInteger()
    }

    /**
     * Renames APK name to contain version, build number and commit hash.
     */
    project.ext.renameApk = { variant ->
        variant.outputs.each { output ->
            File file = output.outputFile
            String gitHash = getGitHash()

            def name = output.outputFile.name.replace("-", ",").replace(project.name + ",",
                    project.parent.name + "-" + project.name + "-" +
                            project.VERSION + "-" + variant.mergedFlavor.versionCode + "-" +
                            gitHash + "-")

            output.outputFile = new File(file.parent, name)
        }
    }

    /**
     * Return last commit git hash string.
     */
    project.ext.getGitHash = {
        // git hash
        def command = Runtime.getRuntime().exec("git rev-parse --short HEAD")
        def result = command.waitFor()
        def hash = (result == 0) ? command.inputStream.text.trim() : "nogit"
        return hash;
    }

    /**
     * Check if project has uncommited changes
     */
    project.ext.checkUncommitedChanges = {
        def command = Runtime.getRuntime().exec("git diff-index --quiet HEAD")
        return command.waitFor() == 1
    }

    project.ext.checkExitValue = { exitValue, expectedValue ->
        if (exitValue != expectedValue) {
            throw new Exception("Failed with exit code " + exitValue.toString())
        }
    }

    project.ext.isWindows = {
        return Os.isFamily(Os.FAMILY_WINDOWS)
    }

    project.ext.isIDEBuild = {
        return (boolean) project.properties['android.injected.invoked.from.ide']
    }
}
